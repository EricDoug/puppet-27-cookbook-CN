[[ch07sec02]]
=== {Recipe702}

使用 ERB 模板配置虚拟主机是一种常见的应用，
因为每个虚拟主机配置的实例通常都使用类似的样板代码，只有一两个变量的值不同而已。 
显然，对于某些网站或应用程序来说，你需要在虚拟主机的定义中指定特殊的配置选项，
然而这些特殊选项又不能通过一个简单的模板来配置 -- 但是，不管怎样，
使用一个模板配置一些简单的站点将会节省时间、避免重复劳动。

==== {Howtodoit}

1. 添加如下代码到 ++/etc/puppet/modules/apache/manifests/init.pp++：
+
----
define site( $sitedomain = "", $documentroot = "" ) {
    include apache

    if $sitedomain == "" {
        $vhost_domain = $name
    } else {
        $vhost_domain = $sitedomain
    }

    if $documentroot == "" {
        $vhost_root = "/var/www/${name}"
    } else {
        $vhost_root = $documentroot
    }

    file { "/etc/apache2/sites-available/${vhost_domain}.conf":
        content => template("apache/vhost.erb"),
        require => File["/etc/apache2/conf.d/name-basedvhosts.conf"],
        notify => Exec["enable-${vhost_domain}-vhost"],
    }

    exec { "enable-${vhost_domain}-vhost":
        command     => "/usr/sbin/a2ensite ${vhost_domain}.conf",
        require     => [ File["/etc/apache2/sites-available/${
          vhost_domain}.conf"], Package["apache2-mpm-prefork"] ],
        refreshonly => true,
        notify      => Service["apache2"],
    }
}
----

2. 使用如下内容创建 +/etc/puppet/modules/apache/templates/vhost.erb+ 文件：
+
----
<VirtualHost *:80>
    ServerName <%= vhost_domain %>
    ServerAdmin admin@<%= vhost_domain %>
    DocumentRoot <%= vhost_root %>
    ErrorLog logs/<%= vhost_domain %>-error_log
    CustomLog logs/<%= vhost_domain %>-access_log common

    <Directory /var/www/<%= vhost_domain %>>
        Allow from all
        Options +Includes +Indexes +FollowSymLinks
        AllowOverride all
    </Directory>
</VirtualHost>

<VirtualHost *:80>
    ServerName www.<%= vhost_domain %>
    Redirect 301 / http://<%= vhost_domain %>/
</VirtualHost>
----

3. 添加如下代码到一个节点：
+
----
apache::site { "keithlard.com": }
----

4. 运行 Puppet：
+
....
# puppet agent --test
info: Retrieving plugin
info: Caching catalog for cookbook.bitfieldconsulting.com
info: Applying configuration version '1309190720'

notice: /Stage[main]//Node[cookbook]/Apache::Site[keithlard.com]/
File[/etc/apache2/sites-available/keithlard.com.conf]/ensure:
defined content as '{md5}f2a558c02beeaed4beb7da250821b663'

info: /Stage[main]//Node[cookbook]/Apache::Site[keithlard.com]/
File[/etc/apache2/sites-available/keithlard.com.conf]: Scheduling
refresh of Exec[enable-keithlard.com-vhost]

notice: /Stage[main]//Node[cookbook]/Apache::Site[keithlard.com]/
Exec[enable-keithlard.com-vhost]: Triggered 'refresh' from 1
events

info: /Stage[main]//Node[cookbook]/Apache::Site[keithlard.
com]/Exec[enable-keithlard.com-vhost]: Scheduling refresh of
Service[apache2]

notice: /Stage[main]/Apache/Service[apache2]: Triggered 'refresh'
from 2 events
notice: Finished catalog run in 3.79 seconds
....


==== {Howitworks}

名为 +apache::site+ 的 +define+ 使用 +vhost.erb+ 模板生成 Apache 虚拟主机的定义。
默认情况下，假设站点的域名与站点实例的名字相同，本例中是 ++keithlard.com++。
所以当 Puppet 看到如下代码时：

----
apache::site { "keithlard.com": }
----

它就使用 +keithlard.com+ 作为站点域名。如果你要指定不同的域名，请添加 +sitedomain+ 参数：

----
apache::site { "networkr_production":
    sitedomain => "networkr.com",
}

apache::site { "networkr_staging":
    sitedomain => "staging.networkr.com",
}
----

模板系统的优秀之处在于：如果你想为所有站点重新配置一个值（例如，更改管理员的 e-mail 地址），
你只需要修改一次模板，Puppet 就会根据模板相应地更新所有的虚拟主机。

同样地，如果你需要为虚拟主机指定与默认值（++/var/www/${name}++）不同的 ++DocumentRoot++，
请添加如下的 +documentroot+ 参数：

----
apache::site { "communitysafety.org":
    documentroot => "/var/apps/commsafe",
}
----

==== {Theresmore}

在前面的例子中，我们只在模板中定义了一个变量，但只要你愿意，你可以使用更多的变量。
它们也可以是 **facts**，例如：

----
ServerName <%= fqdn %>
----

或者 Ruby 表达式：

----
ServerAdmin<%= emails["admin"] %>
----

或者任何你要执行的 Ruby 代码：

----
ServerAdmin <%= vhost_domain == 'coldcomfort.com' ? 'seth@coldcomfort.
com' : 'flora@poste.com' %>
----

==== {Seealso}

- 第 5 章的 <<ch05sec05,{Recipe505}>> 一节
