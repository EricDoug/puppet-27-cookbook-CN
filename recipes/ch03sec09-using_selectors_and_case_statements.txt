[[ch03sec09]]
=== {Recipe309}

[quote, Sign,]
Smarts is the most exclusive club in town. Everyone welcome.

有时选择性是很重要的。尽管你可以使用 +if+ 书写任何条件语句，
但 Puppet 还提供了帮助你更容易地表达条件的额外形式，例如：
选择器（selector）和 case 语句。


==== {Howtodoit}

1. 在你的配置清单中添加如下代码：
+
----
$systemtype = $operatingsystem ? {
    "Ubuntu" => "debianlike",
    "Debian" => "debianlike",
    "RedHat" => "redhatlike",
    "Fedora" => "redhatlike",
    "CentOS" => "redhatlike",
    default  => "redhatlike",
}

notify { "You have a ${systemtype} system": }
----

2. 接下来，再添加如下代码：
+
----
class debianlike {
    notify { "Special manifest for Debian-like systems": }
}

class redhatlike {
    notify { "Special manifest for RedHat-like systems": }
}

case $operatingsystem {
    "Ubuntu",
    "Debian": {
        include debianlike
    }
    "RedHat",
    "Fedora",
    "CentOS": {
        include redhatlike
    }
}
----

==== {Howitworks}

上面的例子同时演示了选择器（selector）和 +case+ 语句，让我们来看看它们是如何工作的。

- *selector*
+
在第一个例子中，我们使用了一个选择器（操作符 ++?++）根据变量  +$operatingsystem+
的值为 +$systemtype+ 变量赋值。 这类似于 C 语言和 Ruby 语言中的三元运算符，
不同之处在于三元运算符只能在两个可选值之间选择，而此处的选择器则可以提供你想要的多个值。
+
Puppet 会与 +$operatingsystem+ 的每一个可能的值（如 Ubuntu、Debian等）一一做比较。
这些值可以是正则表达式（例如，部分字符串匹配，或使用通配符），但在本例中我们仅仅使用了文本字符串。
一旦发现匹配的值，选择器表达式就会返回与其相关的匹配字符串。
例如，如果 +$operatingsystem+ 的值是 Fedora，选择器表达式就会返回字符串
+redhatlike+ 并将其赋予变量 ++$systemtype++。

- *Case* 语句
+
与选择器不同，+case+ 语句不会返回任何值。 +case+ 语句适用于，
当你想根据一个表达式的不同的值执行不同代码的情况。
在我们的第二个例子中，使用 +case+ 语句根据 +$operatingsystem+ 的值或者包含
+debianlike+ 类，或者包含 +redhatlike+ 类。
+
再次指出，Puppet 会根据 +$operatingsystem+ 值与潜在的匹配列表进行比较。
这些匹配列表可以是正则表达式，或者是字符串，或者像我们的例子中一样使用
逗号间隔的字符串列表。
当 Puppet 在匹配列表中找到一个匹配值，就会执行与此匹配项相关的大括号之间的代码。所以，
如果 +$operatingsystem+ 的值是 ++Ubuntu++，那么 +include debianlike+ 就会被执行。

==== {Theresmore}

一旦你掌握了选择器和 +case+ 语句的基本用法，你会发现下面的技巧非常有用。

===== 正则表达式

与 +if+ 语句一样，你可以在选择器和 +case+ 语句中使用正则表达式，并且你也可以
捕获匹配组的值，并使用 ++$1++、++$2++ 等引用它们的值。

----
case $lsbdistdescription {
    /Ubuntu (.+)/: {
        notify { "You have Ubuntu version $1": }
    }
    /CentOS (.+)/: {
        notify { "You have CentOS version $1": }
    }
}
----

===== 默认值

选择器和 +case+ 语句都可以让你指定一个 +default+ 值，当没有其他匹配项时就使用这个默认值：

----
$lunch = "Sausage and chips"
$lunchtype = $lunch ? {
    /chips/ => "unhealthy",
    /salad/ => "healthy",
    default => "unknown",
}

notify { "Your lunch was ${lunchtype}": }
----

结果为：

....
Your lunch was unhealthy
....
